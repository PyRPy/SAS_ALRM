/* Lesson 7: Inferences Regarding Multivariate Population Mean */

* Example 7-1: Woman's Survey Data (Hotelling's Test) Section ;
options ls=78;
title "Hotellings T2 - Women's Nutrition Data";
data nutrient;
  infile "Documents\My SAS Files\STAT505_SAS\MVA_PSU\Data\nutrient.txt";
  input id calcium iron protein a c;
  run;
proc print data = nutrient;
run;
proc iml;
  start hotel;
    mu0={1000, 15, 60, 800, 75};
    one=j(nrow(x),1,1);
    ident=i(nrow(x));
    ybar=x`*one/nrow(x);
    s=x`*(ident-one*one`/nrow(x))*x/(nrow(x)-1.0);
    print mu0 ybar;
    print s;
    t2=nrow(x)*(ybar-mu0)`*inv(s)*(ybar-mu0);
    f=(nrow(x)-ncol(x))*t2/ncol(x)/(nrow(x)-1);
    df1=ncol(x);
    df2=nrow(x)-ncol(x);
    p=1-probf(f,df1,df2);
    print t2 f df1 df2 p;
  finish;
  use nutrient;
  read all var{calcium iron protein a c} into x;
  run hotel;
* reject Ho if p-value < 0.05 ;

proc iml;
one = j(10, 1, 1);
ident = i(10);
print one, ident;
quit;

/* Example 7-3: Women’s Health Survey */
* Simultaneous confidence intervals are computed using hand calculations;

options ls=78;
title "Confidence Intervals - Women's Nutrition Data";
%let p=5;
data nutrient;
  infile "Documents\My SAS Files\STAT505_SAS\MVA_PSU\Data\nutrient.txt";
  input id calcium iron protein a c;
  variable="calcium"; x=calcium; output;
  variable="iron";    x=iron;    output;
  variable="protein"; x=protein; output;
  variable="vit a";   x=a;       output;
  variable="vit c";   x=c;       output;
  keep variable x;
  run;
proc sort;
  by variable;
  run;
proc means noprint;
  by variable;
  var x;
  output out=a n=n mean=xbar var=s2;
  run;
data b;
  set a;
  t1=tinv(1-0.025,n-1);
  tb=tinv(1-0.025/&p,n-1);
  f=finv(0.95,&p,n-&p);
  loone=xbar-t1*sqrt(s2/n);
  upone=xbar+t1*sqrt(s2/n);
  losim=xbar-sqrt(&p*(n-1)*f*s2/(n-&p)/n);
  upsim=xbar+sqrt(&p*(n-1)*f*s2/(n-&p)/n);
  lobon=xbar-tb*sqrt(s2/n);
  upbon=xbar+tb*sqrt(s2/n);
  run;
proc print;
  run;

/* Example 7-4: Women’s Health Survey */
* Here, the 95% confidence interval for calcium under the Bonferroni; 
* correction is calculated;
* see 7-3 results from above ;

/* Example 7-5: Women's Health Survey (Profile Plots) */
options ls=78;
title "Profile Plot - Women's Nutrition Data";
%let p=5;
data nutrient;
  infile "Documents\My SAS Files\STAT505_SAS\MVA_PSU\Data\nutrient.txt";
  input id calcium iron protein a c;
  variable="calcium"; ratio=calcium/1000; output;
  variable="iron";    ratio=iron/15;      output;
  variable="protein"; ratio=protein/60;   output;
  variable="vit a";   ratio=a/800;        output;
  variable="vit c";   ratio=c/75;         output;
  keep variable ratio;
  run;
proc sort;
  by variable;
  run;
proc means;
  by variable;
  var ratio;
  output out=a n=n mean=xbar var=s2;
  run;
data b;
  set a;
  f=finv(0.95,&p,n-&p);
  ratio=xbar; output;
  ratio=xbar-sqrt(&p*(n-1)*f*s2/(n-&p)/n); output;
  ratio=xbar+sqrt(&p*(n-1)*f*s2/(n-&p)/n); output;
  run;
proc gplot;
  axis1 length=4 in;
  axis2 length=6 in;
  plot ratio*variable / vaxis=axis1 haxis=axis2 vref=1 lvref=21;
  symbol v=none i=hilot color=black;
  run;

/* Example 7-9: Spouse Data (Paired Hotelling's) */
options ls=78;
title "Paired Hotelling's T-Square";
data spouse;
  infile "Documents\My SAS Files\STAT505_SAS\MVA_PSU\Data\spouse.txt";
  input h1 h2 h3 h4 w1 w2 w3 w4;
  d1=h1-w1;
  d2=h2-w2;
  d3=h3-w3;
  d4=h4-w4;
  run;
proc print;
  run;
proc iml;
  start hotel;
    mu0={0, 0, 0, 0};
    one=j(nrow(x),1,1);
    ident=i(nrow(x));
    ybar=x`*one/nrow(x);
    s=x`*(ident-one*one`/nrow(x))*x/(nrow(x)-1.0);
    print mu0 ybar;
    print s;
    t2=nrow(x)*(ybar-mu0)`*inv(s)*(ybar-mu0);
    f=(nrow(x)-ncol(x))*t2/ncol(x)/(nrow(x)-1);
    df1=ncol(x);
    df2=nrow(x)-ncol(x);
    p=1-probf(f,df1,df2);
    print t2 f df1 df2 p;
  finish;
  use spouse;
  read all var{d1 d2 d3 d4} into x;
  run hotel;
* This indicates that husbands respond differently on at least;
* one of the questions from their wives;

/* Example 7-10: Spouse Data (Bonferroni CI) */
options ls=78;
title "Confidence Intervals - Spouse Data";
%let p=4;
data spouse;
  infile "Documents\My SAS Files\STAT505_SAS\MVA_PSU\Data\spouse.txt";
  input h1 h2 h3 h4 w1 w2 w3 w4;
  variable=1; diff=h1-w1; output;
  variable=2; diff=h2-w2; output;
  variable=3; diff=h3-w3; output;
  variable=4; diff=h4-w4; output;
  drop h1 h2 h3 h4 w1 w2 w3 w4;
  run;
proc sort;
  by variable;
  run;
proc means noprint;
  by variable;
  var diff;
  output out=a n=n mean=xbar var=s2;
  run;
data b;
  set a;
  f=finv(0.95,&p,n-&p);
  t=tinv(1-0.025/&p,n-1);
  losim=xbar-sqrt(&p*(n-1)*f*s2/(n-&p)/n); 
  upsim=xbar+sqrt(&p*(n-1)*f*s2/(n-&p)/n); 
  lobon=xbar-t*sqrt(s2/n);
  upbon=xbar+t*sqrt(s2/n);
  run;
proc print;
  run;

options ls=78;
title "Profile Plot - Spouse Data";
%let p=4;
data spouse;
  infile "Documents\My SAS Files\STAT505_SAS\MVA_PSU\Data\spouse.txt";
  input h1 h2 h3 h4 w1 w2 w3 w4;
  variable=1; diff=h1-w1; output;
  variable=2; diff=h2-w2; output;
  variable=3; diff=h3-w3; output;
  variable=4; diff=h4-w4; output;
  drop h1 h2 h3 h4 w1 w2 w3 w4;
  run;
proc sort;
  by variable;
  run;
proc means;
  by variable;
  var diff;
  output out=a n=n mean=xbar var=s2;
  run;
data b;
  set a;
  f=finv(0.95,&p,n-&p);
  diff=xbar; output;
  diff=xbar-sqrt(&p*(n-1)*f*s2/(n-&p)/n); output;
  diff=xbar+sqrt(&p*(n-1)*f*s2/(n-&p)/n); output;
  run;
proc gplot;
  axis1 length=4 in;
  axis2 length=6 in;
  plot diff*variable / vaxis=axis1 haxis=axis2 vref=0 lvref=21;
  symbol v=none i=hilot color=black;
  run;

/* Example 7-11: Spouse Data (Question 2) 
  Question 2: Do the husbands and wives accurately perceive 
  the responses of their spouses?
 */
options ls=78;
title "Paired Hotelling's T-Square: Matching Perceptions";
data spouse;
  infile "Documents\My SAS Files\STAT505_SAS\MVA_PSU\Data\spouse.txt";
  input h1 h2 h3 h4 w1 w2 w3 w4;
  d1=h1-w2;
  d2=h2-w1;
  d3=h3-w4;
  d4=h4-w3;
  run;
proc print;
  run;
proc iml;
  start hotel;
    mu0={0, 0, 0, 0};
    one=j(nrow(x),1,1);
    ident=i(nrow(x));
    ybar=x`*one/nrow(x);
    s=x`*(ident-one*one`/nrow(x))*x/(nrow(x)-1.0);
    print mu0 ybar;
    print s;
    t2=nrow(x)*(ybar-mu0)`*inv(s)*(ybar-mu0);
    f=(nrow(x)-ncol(x))*t2/ncol(x)/(nrow(x)-1);
    df1=ncol(x);
    df2=nrow(x)-ncol(x);
    p=1-probf(f,df1,df2);
    print t2 f df1 df2 p;
  finish;
  use spouse;
  read all var{d1 d2 d3 d4} into x;
  run hotel;
* there is no statistically significant evidence against the hypothesis;
* that the husbands and wives accurately perceive the attitudes of ;
* their spouses. 

/* Example 7-13: Swiss Bank Notes (Two-Sample Hotelling's) */

options ls=78;
title "2-Sample Hotellings T2 - Swiss Bank Notes";
data swiss;
  infile "Documents\My SAS Files\STAT505_SAS\MVA_PSU\Data\swiss3.txt";
  input type $ length left right bottom top diag;
  run;
proc iml;
  start hotel2;
    n1=nrow(x1);
    n2=nrow(x2);
    k=ncol(x1);
    one1=j(n1,1,1);
    one2=j(n2,1,1);
    ident1=i(n1);
    ident2=i(n2);
    ybar1=x1`*one1/n1;
    s1=x1`*(ident1-one1*one1`/n1)*x1/(n1-1.0);
    print n1 ybar1;
    print s1;
    ybar2=x2`*one2/n2;
    s2=x2`*(ident2-one2*one2`/n2)*x2/(n2-1.0);
    print n2 ybar2;
    print s2;
    spool=((n1-1.0)*s1+(n2-1.0)*s2)/(n1+n2-2.0);
    print spool;
    t2=(ybar1-ybar2)`*inv(spool*(1/n1+1/n2))*(ybar1-ybar2);
    f=(n1+n2-k-1)*t2/k/(n1+n2-2);
    df1=k;
    df2=n1+n2-k-1;
    p=1-probf(f,df1,df2);
    print t2 f df1 df2 p;
  finish;
  use swiss;
    read all var{length left right bottom top diag} where (type="real") into x1;
    read all var{length left right bottom top diag} where (type="fake") into x2;
  run hotel2;
quit;
* The counterfeit notes can be distinguished from the genuine notes on ;
* at least one of the measurements.;
 
